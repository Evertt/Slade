attribute name  = /[^\s\/>"\'=]+/;
string          = /"[^"\n]+"|'[^'\n]+'/;
variable        = /\$[a-z_]\w*/;
any character except line break and outdent = -(line break | outdent);

static text       = { any character except line break and outdent }
variable text     = { static text | variable | ('{', expression, '}') };

block of static text   = {static text},   [line break, indent, {static text, [line break]},   outdent];
block of variable text = {variable text}, [line break, indent, {variable text, [line break]}, outdent];

text block = '|', block of variable text;
variable block = '=', ['='], (variable | expression), [text block];

language name = 'css' | 'js';
code block    = language name, ':', block of static text;

tag identifier = [tag name], [tag id],    {tag class}
               | [tag name], {tag class}, [tag id];

name              = letter, {letter | digit | '-', '_'};
tag name          = name;
tag id            = '#', name;
tag class         = '.', name;

child blocks = line break, indent, {template block}, outdent;
inline block = ':', template block;

content block     = text block
                  | variable block
                  | yield block
                  | inline block
                  | child blocks;

attribute value   = string | variable;
attribute         = attribute name, ['=', [ '=' | '?' ], attribute value];

tag block = tag identifier, {attribute}, [content block];

html block = '<', block of variable text;

yield block   = '-', name, [content block];
section block = '@', name, [content block];

include block = '+', name, {'.', name}, {attribute}, [content block];
extend block  = '_', name, {'.', name}, {attribute}, [content block];

comment block = '/', ['!'], block of variable text;

iteration block = '>', (variable | expression), ['>', [ variable ]], content block;
conditional block = ('?' | '!'), expression, child blocks;

template block = code block
               | tag block
               | text block
               | html block
               | yield block
               | extend block
               | section block
               | include block
               | comment block
               | variable block
               | iteration block
               | conditional block;

template = {template block, {line break}};